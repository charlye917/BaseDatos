--Elaborado por: Arteaga Lira Carlos Alberto
--Fecha: 19 de mayo del 2018
--Definicion del trigger Instead of para la vista PAIS

Prompt creando triggers para pais
CREATE OR REPLACE TRIGGER TGR_DML_PAIS
INSTEAD OF INSERT OR UPDATE OR DELETE ON V_PAIS
	DECLARE
		V_PAIS_ID NUMBER(5,0);
		V_CLAVE VARCHAR(5);
		V_NOMBRE VARCHAR(40);
		V_REGION CHAR(1);	
	BEGIN
		V_PAIS_ID := :NEW.PAIS_ID;
		V_CLAVE := :NEW.CLAVE;
		V_NOMBRE := :NEW.NOMBRE;
		V_REGION := :NEW.REGION;
		CASE
			WHEN INSERTING THEN
				IF V_REGION = 'A' THEN
					INSERT INTO PAIS_1(PAIS_ID, CLAVE, NOMBRE, REGION)
					VALUES(V_PAIS_ID, V_CLAVE, V_NOMBRE, V_REGION);
				ELSIF V_REGION = 'B' THEN
					INSERT INTO PAIS_2(PAIS_ID, CLAVE, NOMBRE, REGION)
					VALUES(V_PAIS_ID, V_CLAVE, V_NOMBRE, V_REGION);
				ELSE
					RAISE_APPLICATION_ERROR(20001,
						'VALOR INCORRECTO PARA EL CAMPO REGION: '
						|| V_REGION
						|| ' SOLO SE PERMITEN LOS VALORES A, B ');
				END IF;
			WHEN DELETING THEN
				IF :OLD.REGION = 'A' THEN
					DELETE FROM PAIS_1 WHERE PAIS_ID = :OLD.PAIS_ID;
				ELSIF :OLD.REGION = 'B' THEN
					DELETE FROM PAIS_2 WHERE PAIS_ID = :OLD.PAIS_ID;
				ELSE
					RAISE_APPLICATION_ERROR(-20001,
						'VALOR INCORRECTO PARA EL CAMPO REGION: '
						|| :OLD.REGION
						|| ' SOLO SE PERMITEN LOS VALORES A, B');
				END IF;
			WHEN UPDATING THEN
				--EL REGISTRO SE QUEDA EN EL SITIO 1
				IF V_REGION = 'A' AND :OLD.REGION = 'A'THEN
					UPDATE PAIS_1 SET PAIS_ID = V_PAIS_ID,
					CLAVE = V_CLAVE, NOMBRE = V_NOMBRE, REGION=V_REGION
					WHERE PAIS_ID = :OLD.PAIS_ID;
				--EL REGISTRO CAMBIA DE SITIO 2 A 1
				ELSIF V_REGION = 'A' AND :OLD.REGION = 'B' THEN
					DELETE FROM PAIS_2 WHERE PAIS_ID = :OLD.PAIS_ID;
					INSERT INTO PAIS_1 (PAIS_ID, CLAVE, NOMBRE, REGION)
					VALUES(V_PAIS_ID, V_CLAVE, V_NOMBRE, V_REGION);
				--EL REGISTRO CAMBIA DE SITIO 1 A 2
				ELSIF V_REGION = 'B' AND :OLD.REGION = 'A' THEN
					DELETE FROM PAIS_1 WHERE PAIS_ID = :OLD.PAIS_ID;
					INSERT INTO PAIS_2 (PAIS_ID, CLAVE, NOMBRE, REGION)
					VALUES(V_PAIS_ID, V_CLAVE, V_NOMBRE, V_REGION);
				--EL REGISTRO SE QUEDA EN EL SITIO 2
				ELSIF V_REGION = 'B' AND :OLD.REGION = 'B'THEN
					UPDATE PAIS_2 SET PAIS_ID = V_PAIS_ID,
					CLAVE = V_CLAVE, NOMBRE = V_NOMBRE, REGION=V_REGION
					WHERE PAIS_ID = :OLD.PAIS_ID;
				else
					raise_application_error(-20001,
					'Valor incorrecto para el campo REGION : '
					|| V_REGION
					|| ' Solo se permiten los valores A , B ');
				end if;		
		END CASE;
	END;
/
prompt listo

